theory registration_and_authentication_in_the_basic_mode
begin

// The user enters his/her email in the client device. The request is sent to the home server.
// The home server sends an email with a verification link to the user (https link). The link contains a
// verification code that is valid for 10 minutes. If the email does not exist, the home server stores it as unverified
// if it is not verified within 10 min, it is removed
rule fresh_verification_code:
     [Fr(~link)]
     -->
     [VerificationEmail($email, ~link), ServerSentEmail($email, ~link)]

rule read_email:
     [VerificationEmail($email, ~link)]
     --[CompromiseEmail()]->
     [Out(~link), VerificationEmail($email, ~link)]

rule write_email:
     [In(link)]
     --[CompromiseEmail()]->
     [VerificationEmail($email, link)]

// The client clicks on the link (a GET request).
rule click_on_link:
     [VerificationEmail($email, link)]
     -->
     [ClickOnLink(link)]

// The server verifies the verification secret and returns either fail, or if successful, a tuple (u, k^session_u)
// where u is the user identifier and ksession u
// is the new session key that can be used in further requests. If it was a new user, the server marks the email as verified. The user is now authenticated and registered (if it was not already registered).
rule verify_user:
     [ClickOnLink(link), ServerSentEmail($email, link), Fr(~sessionKey), !User(userid, $email)]
     --[SessionKey(userid, ~sessionKey), NewUser('f')]->
     [Session(userid, ~sessionKey)]

rule create_user:
     [ClickOnLink(link), ServerSentEmail($email, link), Fr(~sessionKey), Fr(~userid)]
     --[SessionKey(~userid, ~sessionKey), NewUser('t')]->
     [Session(~userid, ~sessionKey), !User(~userid, $email)]

rule reveal_user_email:
     [!User(userid, $email)]
     --[UserEmailReveal(userid, $email)]->
     [Out(userid)]

//Lemma: User is registered or authenticated after process
lemma new_user_can_start_session:
      exists-trace
        "Ex user key #i.
            SessionKey(user, key) @i & NewUser('t') @i"

lemma existing_user_can_start_session:
      exists-trace
        "Ex user key #i.
            SessionKey(user, key) @i & NewUser('f') @i"

lemma existing_user_has_been_created:
      "All user key1 #i.
      SessionKey(user, key1) @i & NewUser('f') @i ==>
            Ex key2 #j.
               SessionKey(user, key2) @j & NewUser('t') @j
               & j < i"

//If the adversary can not read the user's email, it can not obtain their sessionkey
lemma can_not_compromise_session_key:
      "All user key1 #i.
           SessionKey(user, key1) @i
           & not(Ex #r. CompromiseEmail() @r)
           ==>
           not (Ex #j.
           K(key1) @j)"


end
